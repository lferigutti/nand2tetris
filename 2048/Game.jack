class Game {
  field Board board; // Board of the game itself
  field int size; // Size of the board. Hardcoded by now
  field int initScreenRow, initScreenCol; // Initical position of the board in the screen

  constructor Game new() {
    let size = 4;
    let board = Board.new(size);
    let initScreenRow = 6;
    let initScreenCol = 20;
    return this;
  }

  /* Here is the logic of the game */
  method void run() {
    do draw();
    return;
  }

  /* This method is in charge of undestanding where to draw and what to draw. */
  method void draw() {
    var int rowIndex;
    var int screenRow, screenCol;
    
    let rowIndex = 0;
    let screenRow = initScreenRow;
    let screenCol = initScreenCol;

    while (rowIndex < size) {
      do Output.moveCursor(screenRow, screenCol);
      do Output.printString("+-----+-----+-----+-----+");
      let screenRow = screenRow + 1;
      do drawRow(rowIndex, screenRow, screenCol);
      let rowIndex = rowIndex + 1;
      let screenRow = screenRow + 1;
    }
    
    // Draw last line
    do Output.moveCursor(screenRow, screenCol);
    do Output.printString("+-----+-----+-----+-----+");
    return;
  }

  /* This method draws only one row */
  method void drawRow(int rowIndex, int screenRow, int screenCol) {
    var int col, value;
    var String cellStr;

    do Output.moveCursor(screenRow, screenCol);
    do Output.printString("|");
    let col = 0;
    while (col < size) {
      let value = board.get(rowIndex, col);
      let cellStr = getCellString(value);

      do Output.printString(cellStr);
      do Output.printString("|");
      
      let col = col + 1;
    }
    return;
  }


  /* Returs a tile from a value -> it is tedius but follows the limitaiton of jack language */
  method String getCellString(int value) {

    if (value = 0) {
      return "     ";
    }
    if (value = 2) {
      return "  2  ";
    }
    if (value = 4) {
      return "  4  ";
    }
    if (value = 8) {
      return "  8  ";
    }
    if (value = 16) {
      return " 16  ";
    }
    if (value = 32) {
      return " 32  ";
    }
    if (value = 64) {
      return " 64  ";
    }
    if (value = 128) {
      return " 128 ";
    }
    if (value = 256) {
      return " 256 ";
    }  
    if (value = 512) {
      return " 512 ";
    }
    if (value = 1024) {
      return "1024 ";
    }  
    if (value = 2048) {
      return "2048 ";
    }
    return "     ";
  }

  /** Disposes this game */
  method void dispose() {
    do board.dispose();
    do Memory.deAlloc(this);
    return;
  }

}


